plugins {
    id 'com.android.application'
}

android {
    namespace 'io.github.chipppppppppp.lime'
    compileSdk 34

    defaultConfig {
        minSdk 28
        targetSdk 34
        versionCode 17
        versionName "1.12.0"
        multiDexEnabled false
        proguardFiles += 'proguard-rules.pro'
        buildConfigField 'String', 'HOOK_TARGET_VERSION', '"150000454"'
    }

    signingConfigs {
        android {
            storeFile file('android.jks')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
        release {
            storeFile file('release.jks')
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {
        configureEach {
            signingConfig signingConfigs.android
        }
        release {
            if (file('release.jks').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    lint {
        checkReleaseBuilds false
    }

    buildFeatures{
        buildConfig true
        compose false
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    aaptOptions {
        additionalParameters '--allow-reserved-package-id', '--package-id', '0x64'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly files('libxposed/api-100.jar')
    //noinspection GradleCompatible
    implementation 'com.android.support:customtabs:23.0.0'
    implementation 'org.mozilla:rhino:1.7.15'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
