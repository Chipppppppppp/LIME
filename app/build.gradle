plugins {
    id 'com.android.application'
}

android {
    namespace 'io.github.chipppppppppp.lime'
    compileSdk 34

    buildFeatures.buildConfig = true

    defaultConfig {
        minSdk 28
        //noinspection OldTargetApi
        targetSdk 33
        versionCode 12
        versionName "1.9.1"
        multiDexEnabled false
        proguardFiles += 'proguard-rules.pro'
        buildConfigField 'String', 'HOOK_TARGET_VERSION', '"14.9.1"'
    }

    signingConfigs {
        android {
            storeFile file('android.jks')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
        release {
            storeFile file('release.jks')
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {
        configureEach {
            signingConfig signingConfigs.android
        }
        release {
            minifyEnabled true
            shrinkResources true
            if (file('release.jks').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    lint {
        checkReleaseBuilds false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    aaptOptions {
        aaptOptions.additionalParameters '--allow-reserved-package-id', '--package-id', '0x64'
    }
}

dependencies {
    //noinspection GradleCompatible
    implementation 'com.android.support:customtabs:23.0.0'
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'org.mozilla:rhino:1.7.15'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
